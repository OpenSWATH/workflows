# Obtain run_ids from DDA or DIA-ps mzXML files in data folder
run_ids, = glob_wildcards("../data_dda/{run}.mzXML")

rule all:
    input:
        global_target_pqp =  expand("../results/library/{run}_global_pqp.tsv", run=run_ids),
        pqp = "../data_library/library.pqp"

rule decoydb:
    input:
        "../data_library/library.fasta"
    output:
        "../results/library/library.fasta"
    shell:
        "DecoyDatabase -in {input} -out {output}"

rule spectra_link:
    input:
        "../data_dda/{run}.mzXML"
    output:
        temp("../results/library/{run}.mzXML")
    shell:
        "ln {input} {output}"

rule spectra_convert:
    input:
        "../results/library/{run}.mzXML"
    output:
        "../results/library/{run}.mzML"
    shell:
        "outdir=$(dirname {output}) && "
        "msconvert {input} --32 -o $outdir"

rule msfragger:
    input:
        fasta = rules.decoydb.output,
        mzxml = rules.spectra_link.output
    output:
        "../results/library/{run}.pepXML"
    threads: 4
    resources:
        mem_mb=lambda wildcards, attempt: attempt * 8192
    shell:
        "java -Xmx8G -jar MSFragger-20171106.jar fragger.params {input.mzxml}"

rule peptideprophet:
    input:
        fasta = rules.decoydb.output,
        pepxml = rules.msfragger.output
    output:
        "../results/library/interact-{run}.pepXML"
    shell:
        "InteractParser {output} {input.pepxml} -D{input.fasta} && PeptideProphetParser {output} ACCMASS NONPARAM PPM DECOY=DECOY_"

rule iprophet:
    input:
        expand("../results/library/interact-{run}.pepXML", run=run_ids)
    output:
        "../results/library/iprophet.pepXML"
    threads: 4
    shell:
        "InterProphetParser DECOY=DECOY_ THREADS={threads} {input} {output}"

rule easypqp:
    input:
        pepxml = rules.iprophet.output,
        mzml = expand("../results/library/{run}.mzML", run=run_ids),
    output:
        temp(touch("calibrated.iRT"))
    params:
        library_modifications = "library_modifications.params",
        fdr_threshold = 0.01
    threads: 1
    script:
        "scripts/easypqp.py"

rule global_target_pqp:
    input:
        iRT = "calibrated.iRT"
    params:
        peaks = "../results/library/{run}_global_peaks.tsv"
    output:
        temp("../results/library/{run}_global_pqp.tsv")
    shell:
        "OpenSwathAssayGenerator -in {params.peaks} -out {output}"

rule global_combined_pqp:
    input:
        expand("../results/library/{run}_global_pqp.tsv", run=run_ids)
    output:
        temp("../results/library/combined_global_pqp.tsv")
    shell:
        "awk 'BEGIN {{ FS=\"\t\"; OFS=\"\t\" }} FNR>1 || NR==1 {{print $1,$2,$3,$4,$5,$6,$7,$8,$14,$16,$17}}' {input} > {output}"

rule global_combined_decoy_pqp:
    input:
        rules.global_combined_pqp.output
    output:
        "../data_library/library.pqp"
    shell:
        "OpenSwathDecoyGenerator -in {input} -out {output}"
